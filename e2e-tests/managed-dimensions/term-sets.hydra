PREFIX md: <https://cube-creator.zazuko.com/managed-dimensions/vocab#>
PREFIX hydra: <http://www.w3.org/ns/hydra/core#>
PREFIX schema: <http://schema.org/>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX meta: <https://cube.link/meta/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

ENTRYPOINT "managed-dimensions/"

HEADERS {
  x-user "john-doe"
  x-permission "pipelines:write"
}

With Class hydra:Resource {
    Expect Link md:managedDimensions {
        Expect Type md:ManagedDimensions
        Expect Property hydra:member {
            Expect Property md:terms
        }

        Expect Operation schema:CreateAction {
            Invoke {
                Content-Type "text/turtle"

                ```
                prefix schema: <http://schema.org/>
                prefix xsd: <http://www.w3.org/2001/XMLSchema#>
                prefix qudt: <http://qudt.org/schema/qudt/>
                prefix meta: <https://cube.link/meta/>
                prefix sh: <http://www.w3.org/ns/shacl#>

                <> schema:name "Categories"@en ;
                   schema:validFrom "2020-02-20T00:00:00"^^xsd:dateTime ;
                   sh:property [
                     schema:name "Categories"@en ;
                     qudt:scaleType qudt:NominalScale ;
                     meta:dataKind [ a schema:GeoShape ] ;
                   ] ;
               .
                ```
            } => {
                Expect Status 201

                Expect Type meta:SharedDimension
                Expect Property sh:property {
                    Expect Property schema:name
                    Expect Property qudt:scaleType
                    Expect Property meta:dataKind
                }
            }

            Invoke {
                Content-Type "text/turtle"

                ```
                prefix schema: <http://schema.org/>

                <> schema:validFrom "2020-02-20T00:00:00"^^xsd:dateTime .
                ```
            } => {
                Expect Status 400
            }
        }
    }
}
